---
layout: post
title: Java Exception
tag: Java
---

## 前言
最近在看《深入理解 Java 虚拟机》这本书的类文件结构部分产生了兴趣，之前也没多做了解关于 class 文件相关，所以在此做个小总结。

### java 的异常处理
首先来看一段代码:
```java
public static int inc(){
    int x;
    try{
        x = 1;
        return x;
    }catch (Exception e){
        x = 2;
        return x;
    }finally {
        x = 3;
    }
}
```
众所周知，这段代码如果没有异常会返回 1，出现可 ```catch``` 的异常会返回 2，出现无法```catch```的异常程序终止，无返回值。一旦在 ```finally``` 最后加入 ```return x;```则只会返回 3。

如果将基本数据类型改为 ```class```，如下所示:
```java
private static IntTest exceptionTest(){

    IntTest intTest = new IntTest();
    try{
        intTest.setNumber(1);
        return intTest;
    }catch (Exception e){
        intTest.setNumber(2);
        return intTest;

    }finally {
        // intTest = new IntTest();
        intTest.setNumber(3);
    }
}

private static class IntTest{
    private int number;

    void setNumber(int number) {
        this.number = number;
    }

    int getNumber(){
        return this.number;
    }
}
```
此时当然返回就是 number 为 3 的 ```IntTest```，因为我们都知道在执行 ```return``` 之前会调用 ```finally``` 方法，那么如果将注释打开，在```finally```中 ```new Intest()```并赋值，返回值会是什么呢？

答案是: 如果无异常，返回是 number 为 1 的 ```IntTest```；出现可捕获的异常，返回是 number 为 2 的 ```IntTest```，出现无法```catch```的异常程序终止，无返回值。

具体是怎么回事呢？

首先用 ```javac``` 编译 ```XXX.java``` 文件，得到 ```XXX.class``` 文件，然后用 ```javap -v (-verbose) XXX``` 查看编译后的 ByteCode 字节码和异常表。

注意 javap 默认不会显示 private 的方法，需要显示需要加参数 -p (-private)

```java
public static int inc();
    descriptor: ()I
    flags: ACC_PUBLIC, ACC_STATIC // 表示方法是 public static 的
    Code:
      stack=1, locals=4, args_size=0
         0: iconst_1    // 1(int) 值入栈 (try 块中的 x = 1)
         1: istore_0    // 将栈顶 int 类型值保存到局部变量 0 中 
         2: iload_0     // 从局部变量 0 中装载 int 类型值入栈 (保存 x 到 returnValue 中，此时 x = 1)
         3: istore_1    // 将栈顶 int 类型值保存到局部变量 1 中。
         4: iconst_3    // 3(int) 值入栈 （finally 块中的 x = 3）
         5: istore_0    // 将栈顶 int 类型值保存到局部变量 0 中 
         6: iload_1     // 从局部变量 1 中装载 int 类型值入栈 (将 returnValue 中的值放到栈顶，准备给 ireturn 返回)
         7: ireturn     // 返回 int 类型值。
         8: astore_1    // 将栈顶引用类型值保存到局部变量 1 中 (给 catch 中定义的 Exception e 赋值，存储在 Slot2 中)
         9: iconst_2    // 2(int) 值入栈 (catch 块中的 x = 2）
        10: istore_0    // 将栈顶 int 类型值保存到局部变量 0 中。
        11: iload_0     // 从局部变量 0 中装载 int 类型值入栈 (保存 x 到 returnValue 中，此时 x = 2)
        12: istore_2    // 将栈顶 int 类型值保存到局部变量 2 中 (将 returnValue 中的值放到栈顶，准备给 ireturn 返回)
        13: iconst_3    // 3(int) 值入栈 （finally 块中的 x = 3）
        14: istore_0    // 将栈顶 int 类型值保存到局部变量 0 中。
        15: iload_2     // 从局部变量 2 中装载 int 类型值入栈。
        16: ireturn     // 返回 int 类型值。
        17: astore_3    // 将栈顶引用类型值保存到局部变量 3 中 (如果出现了不属于 java.lang.Exception 及其子类的异常才会走到这里)
        18: iconst_3    // 3(int) 值入栈 （finally 块中的 x = 3）
        19: istore_0    // 将栈顶 int 类型值保存到局部变量 0 中。
        20: aload_3     // 从局部变量 3 中装载引用类型值入栈 (将异常放置到栈顶)
        21: athrow      // 抛出异常。
      Exception table:
         from    to  target type
             0     4     8   Class java/lang/Exception
             0     4    17   any
             8    13    17   any
```
编译器为这段 java 源码生成了 3 条异常表记录，对应 3 条可能出现的代码执行路径。从 java 代码的语义上讲，这三条执行路径分别为:

* 如果 try 块中出现 Exception 及其子类的异常，转到 catch 块处理
* 如果 try 块中出现不属于 Exception 及其子类的异常，转到 finally 块处理
* 如果 catch 块中出现任何异常，转到 catch 块处理

回到之前提出的问题，从字节码上可以看到每次在执行 ```finally``` 块的语句前，即 ```iconst_3``` 这句(在 4、 13、 18 行)都会调用 ```istore``` 或者 ```astore``` 在局部变量保存当前应该返回的值或者应抛出的异常，再去执行 ```finally``` 中的代码，然后再将局部变量保存的值返回。


而在 finally 中的 ```intTest = new IntTest();``` 在 return 时未生效，就是因为 java 是值传递。以下为 ```exceptionTest()``` 的字节码。可以看出每次执行 ```finally``` 之前都会 ```astore``` 一下当前的 ```intTest``` 对应的对象。
```java
private static JavaCodeTest$IntTest exceptionTest();
    descriptor: ()LJavaCodeTest$IntTest;
    flags: ACC_PRIVATE, ACC_STATIC
    Code:
      stack=3, locals=4, args_size=0
         0: new           #6                  // class JavaCodeTest$IntTest
         3: dup
         4: aconst_null
         5: invokespecial #7                  // Method JavaCodeTest$IntTest."<init>":(LJavaCodeTest$1;)V
         8: astore_0
         9: aload_0
        10: iconst_1
        11: invokevirtual #8                  // Method JavaCodeTest$IntTest.setNumber:(I)V
        14: new           #9                  // class java/lang/RuntimeException
        17: dup
        18: invokespecial #10                 // Method java/lang/RuntimeException."<init>":()V
        21: athrow
        22: astore_1
        23: aload_0
        24: iconst_2
        25: invokevirtual #8                  // Method JavaCodeTest$IntTest.setNumber:(I)V
        28: getstatic     #2                  // Field java/lang/System.out:Ljava/io/PrintStream;
        31: ldc           #11                 // String 2 ing
        33: invokevirtual #12                 // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        36: aload_0
        37: astore_2
        38: new           #6                  // class JavaCodeTest$IntTest
        41: dup
        42: aconst_null
        43: invokespecial #7                  // Method JavaCodeTest$IntTest."<init>":(LJavaCodeTest$1;)V
        46: astore_0
        47: aload_0
        48: iconst_3
        49: invokevirtual #8                  // Method JavaCodeTest$IntTest.setNumber:(I)V
        52: getstatic     #2                  // Field java/lang/System.out:Ljava/io/PrintStream;
        55: ldc           #13                 // String 3 ing
        57: invokevirtual #12                 // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        60: aload_2
        61: areturn
        62: astore_3
        63: new           #6                  // class JavaCodeTest$IntTest
        66: dup
        67: aconst_null
        68: invokespecial #7                  // Method JavaCodeTest$IntTest."<init>":(LJavaCodeTest$1;)V
        71: astore_0
        72: aload_0
        73: iconst_3
        74: invokevirtual #8                  // Method JavaCodeTest$IntTest.setNumber:(I)V
        77: getstatic     #2                  // Field java/lang/System.out:Ljava/io/PrintStream;
        80: ldc           #13                 // String 3 ing
        82: invokevirtual #12                 // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        85: aload_3
        86: athrow
      Exception table:
         from    to  target type
             9    22    22   Class java/lang/RuntimeException
             9    38    62   any
      LineNumberTable:
        line 12: 0
        line 15: 9
        line 17: 14
        line 18: 22
        line 19: 23
        line 20: 28
        line 21: 36
        line 24: 38
        line 25: 47
        line 27: 52
        line 21: 60
        line 24: 62
        line 25: 72
        line 27: 77
        line 28: 85
      StackMapTable: number_of_entries = 2
        frame_type = 255 /* full_frame */
          offset_delta = 22
          locals = [ class JavaCodeTest$IntTest ]
          stack = [ class java/lang/RuntimeException ]
        frame_type = 103 /* same_locals_1_stack_item */
          stack = [ class java/lang/Throwable ]
```
